plugins {
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.7.5'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.7'
    implementation 'io.swagger.core.v3:swagger-models:2.2.7'
    implementation 'io.springfox:springfox-swagger2:3.0.0'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:6.2.0'
}

swaggerSources {
    petstore {
        inputFile = file("$rootDir/docs/openapi/apiDoc.yml")
        code {
            language = 'spring'
            configFile = file("$rootDir/docs/openapi/java-spring-config.json")
            components = ['models', 'apis', 'supportingFiles']
            dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.petstore.code
sourceSets.main.java.srcDir "${swaggerSources.petstore.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.petstore.code.outputDir}/src/main/resources"

test {
    useJUnitPlatform()
}